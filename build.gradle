plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.4-SNAPSHOT" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        if (project.name == "forge") {
            mappings loom.layered() {
                it.mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
                it.mappings file("${rootDir}/mappings/yarnpatch.tiny")
            }
        } else {
            mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = rootProject.mod_id
    version = "${rootProject.mod_version}+${rootProject.minecraft_version}"
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven { url "https://jitpack.io" }
        maven { url "https://maven.isxander.dev/releases" }
        maven { url "https://maven.shedaniel.me/" }
        maven { url "https://maven.terraformersmc.com/" }
        maven { url "https://maven.quiltmc.org/repository/release" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }

        flatDir { dir "${rootProject.getProjectDir()}/lib" }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

task publishMod { }
